//Global story tags
# title: Кірмаш у Лесе
# frequency: Uncommon
# development: false
# illustration: trader

INCLUDE include.ink

VAR HorsePrice = 2000
VAR FoodPrice = 10
VAR HorseBetPrice = 500
VAR HorseBetPayout = 2500
VAR WinHorseRace = 0
    ~ WinHorseRace = RANDOM(0,1)
VAR TurnipPrice = 50

-> Start

===Start===
Падчас падарожжа вашага войска на паляне нечакана адкрываецца дзіўнае гледзішча -  шумны кірмаш, вядомы як Моррсьлібская Весялосьць. Намёты ганарліва ўзвышаюцца, іх колеры скачуць у праменях сонца. Сьмех зьмешваецца з рыгатаньнем коней - сэрца гэтага радаснага сходу.

Гандляры вабяць, іх вочы гараць свавольствам, яны прадаюць коней са зьніжкай у параўнаньні са звычайным коштам, які льга знайсьці ў скрутках. У паветры лунаюць спакусьлівыя водары печанага мяса, пеністага элю і даўкага віна. Сярод вясёлага натоўпу ўхмыляецца земляроб, што прапаноўвае рэпу, якая дзіўна напамінае знакамітую двуххвостую камету. За сапраўдныя капейкі вы атрымаеце шанец стаць уладальнікам гэтага дзіўнага цуда.
    ->choices

    =choices
    * [Далучыцца да натоўпу на конным кірмашы.]->HorseStalls
    * [Атрымаць асалоду ад водару кірмаша.]->FoodStalls
    * [Выпрабаваць сваё шчасьце на конных скачках.]->HorseRaces
    * [Разглядзець дзіўную рэпу.]->Turnip
    * [Працягнуць падарожжа праз лес.]->Leave

===HorseStalls===
Сэрца кірмашу б'ецца наймацней на конным кірмашу. Ганарлівыя жарабцы скачуць, вочы іх лютыя і дзікія. Адзін конь, у прыватнасьці, прыцягвае вашую ўвагу. Ён мае гладкую поўсьць колеру чорнага дрэва, што зіхаціць у сонечных праменях, а ў вачах, здаецца, застыў дасьведчаны бляск. 
    ->choices

    =choices
    + [Пагадзіцца на ўгоду гандляра па каню. ({HorsePrice} золата)]->BuyHorse
    * [Пераканаць гандляра зьнізіць кошт. {print_player_skill_chance("Charm",150)}]->PersuadeMerchant
    * [Вярнуцца да сэрца кірмаша.]->Start.choices

===BuyHorse===
{HasEnoughGold(HorsePrice): Вы складаеце ўгоду з гандляром. Вы мяняеце манеты на трывалае сядло і вупраж. У прадчуваньні вы сядаеце на каня. Сувязь паміж вамі ўзьнікае імгненна, конь рэагуе на вашыя дакрананьні з даверам і гатовасьцю. {GiveGold(-HorsePrice)} | Вам бракуе золата.}

* [Вярнуцца да весялосьці]->Start.choices

===PersuadeMerchant===
{perform_player_skill_check("Charm",150): -> success | -> fail}

    =success
    Вашыя словы робяць сваю магію і гандляр пагаджаецца зьнізіць кошт на 25%. Гандляр бурчыць, але шануе вашае ўменьне весьці перамовы.
    ~HorsePrice = 1500
    ->HorseStalls.choices

    =fail
    Нягледзячы на ўсе вашыя спробы патаргавацца, гандляр упарта трымае кошт.
    ->HorseStalls.choices
    

===FoodStalls===
Водары лунаюць і вабяць, накіроўваючы вас на баляваньне густаў. Мяса шыпіць, эль пеніцца - карнавал пачуцьцяў. Ежы тут больш чым трэба, і выбар за вамі.

* [Прыняць удзел у кірмашовым фэсьце. ({FoodPrice} золата)]->BuyFood
* [Ісьці далей, не паддаючыся спакусе.]->Start

===BuyFood===
{HasEnoughGold(FoodPrice): Спакуса перамагае. Вы балюеце, і смакі кірмаша гучаць на вашым языке цудоўнай сімфоніяй. Гандляры ўхвальна ківаюць вам усьлед. {GiveGold(-FoodPrice)} | Вам бракуе золата.}

* [Вярнуцца да весялосьці.]->Start.choices

===HorseRaces===
З амфітэатра даносяцца ўхвальныя воклічы. Коні ржуць, вершнікі заклікаюць іх да славы.
->choices

    =choices
    * [Зрабіць стаўку на скакавога каня. ({HorseBetPrice} золата - узнгарода 5x пры перамозе)]->PlaceBet
    * [Вы вырашылі не выпрабоўваць сваю ўдачу.]->Start.choices

===PlaceBet===
{not HasEnoughGold(HorseBetPrice): Вам бракуе золата. -> HorseRaces.choices}
~GiveGold(-HorseBetPrice)
{WinHorseRace: ->success | ->fail}
    =success
    Ваша сэрца б'ецца, калі вы робіце стаўку. Абраны вамі конь ірвецца наперад, і посьпех танцуе на вашую карысьць. Сьмех і звон манет атачаюць вас.
    ~ GiveGold(HorseBetPayout)
    * [Вярнуцца да натоўпу весялосьці.]->Start.choices

    =fail
    Ваша сэрца б'ецца, калі вы робіце стаўку. Абраны Вамі конь спачатку хутка выдзіраецца наперад, але неўзабаве яго даганяюць іншыя наезьнікі. Урэшце, Вашы конь запавольваецца гэтак, што можа заняць толькі апошняе месца. Сьмех і звон манет атачаюць вас.
    
    * [Вярнуцца да натоўпу весялосьці.]->Start.choices

===Turnip===
Інтрыга зацягвае вас, калі вы глядзіце на рэпу ў форме каметы - мудрагелістае дзіва. Земляроб усьміхаецца, запрашаючы вас прыняць удзел у латарэі.
    ->choices

    =choices
    * [Выпрабаваць сваю ўдачу, купіўшы латарэйны квіток. ({TurnipPrice} золата)]->BuyTicket
    * [Выкарыстаць сваё ўспрыманьне, каб знайсьці ўтоеныя падказкі пра рэпу. {print_player_skill_chance("Roguery", 80)}]->PerceiveTurnip
    * [Пайсьці далей, пакінуўшы цікавую рэпу ззаду.]->Start.choices

===BuyTicket===
{HasEnoughGold(TurnipPrice): Кінуўшы манету і ўсьміхнуўшыся, вы забясьпечваеце сабе шанец на ўдзел у розыгрышы. Хто ведае? Можа, рэпа, пацалаваная каметай, усё-такі стане вашай. {GiveGold(-TurnipPrice)} | Вам бракуе золата. -> Turnip.choices}

У прадчуваньні латарэі пачынаецца розыгрыш, і калі дыктар кліча нумар пераможнага квітка, вы затойваеце дыханьне. Аднак гэтым разам посьпех не на вашым боку. Пераможны нумар не ваш, і вас ахапляе пачуцьцё расчараваньня.

* [Вярнуцца да баляваньня весялосьці.]->Start.choices

=== PerceiveTurnip ===
{perform_player_skill_check("Roguery", 80): -> success | ->fail}

    =success
    Ваша вострае вока ловіць тонкія дэталі, якія іншыя могуць прапусьціць. На рэпе няма ніякіх дзіўных азнак, што намякаюць на яе значнасьць. Яна зусім звычайная.
    ->Turnip.choices
    
    =fail
    Вашы агляд ня выявіў нічога незвычайнага ў рэпе.
    ->Turnip.choices

===Leave===
Калі весялосьць кірмашу сьціхае, вы вяртаецеся ў абдымкі дзікай прыроды, пакідаючы за сабой сьмех кірмашовай весялосьці.
->END
