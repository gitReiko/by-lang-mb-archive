//Global story tags
# title: Майстэрства Клінка
# frequency: Special
# development: false
# illustration: roadpoint2

INCLUDE include.ink

VAR PlayerWin = false
VAR MetBefore = true
    ~ MetBefore = GetPlayerHasCustomTag("MetVittorio")
VAR DeniedBefore = true
    ~ DeniedBefore = GetPlayerHasCustomTag("DeniedVittorio")

    ~ SetTextVariable("MetBefore",MetBefore)
    ~ SetTextVariable("DeniedBefore1",DeniedBefore)
    ~ SetTextVariable("DeniedBefore2",DeniedBefore)
    ~ SetTextVariable("MetBefore2",MetBefore)

->START

===START===
Вы працягваеце свой шлях па завілістай дарозе, і кожны вашы крок суправаджаецца ціхім шэпатам ветру.#STR_Start1
Раптам вашую ўвага прыцягнуў рытмічны грук крокаў, што набліжаліся. Падняўшы вочы, {not MetBefore: вы заўважылі таямнічую фігуру, што ўпэўнена набліжалася да вас. По меры набліжэньня, у вочы кінуўся бляск рапіры тонкай работы. Незнаёмы спыніўся перад вамі і пацаў ацэньваць вас, на яго твары з'явілася цёплая ўсьмешка.}{MetBefore: вы заўважылі знаёмую фігуру Вітторыё дэ Люка, знакамітага Цілійскага дуэлянта, які ўпэўнена крочыў да вашай групы. {not DeniedBefore: Успаміны пра папярэднюю дуэль наваліліся на яго хваляй, і ў галаве дагэтуль не заціхала захапленьне ад першай дуэлі.}} #illustration: stranger #STR_Start2
{not MetBefore: "Вох, якое шчасьце, сустрэць на гэтым самотным шляхе вартых ваяроў - гавора ён: я Вітторыё дэ Люка, майстар клінка із далёкіх зямель Ціліі. Я прайшоў доўгую дарогу ў пошуках вартага суперніка, здольнага параўнацца з маім майстэрствам у боі. І вось доля прывяла мяне да вас. Жадаеце спраўдзіць сваё майстэрства ў сяброўскім боі, склаўшы маленькі заклад, каб было цікавей?"#STR_Start3NotMetBefore}
{MetBefore: Пакуль Вітторыё набліжаецца, у вочы кідаецца бляск яго тонка вырабленай рапіры, і на яго твары з'яўляецца цёплая ўсьмешка, калі ён ацэньвае вас. "Вох, якое шчасьце, зноў сустрэцца на гэтым самотным шляхе - гавора ён з гарэзьлівым адценьнем у голасе: Бачу, у тваім сэрцы дагэтуль гарыць полымя ваяра. {not DeniedBefore: Жадаеш наноў спраўдзіць сваё майстэрства ў рэваншы?"} {DeniedBefore:  Жадаеце давесьці сваё майстэрства ў гэты раз?"}#STR_Start3MetBefore}
~ SetPlayerCustomTag("MetVittorio")
-> choices

=choices
*[Прыняць выклік.] ->accept
*[Магчыма іншым разам. Мы ня можам марнаваць час.] -> deny

=accept
{not MetBefore: Заінтрыгаваныя прапановай, вы адказваеце на яго ўсмешку, цікавасьцю да прапанаваных закладаў. #STR_AcceptNotMetBefore1}
{not MetBefore: "Дуэль з закладам? Я слухаю" - адказваеце вы, адкрываючыся да ідэі. #STR_AcceptNotMetBefore2}
{not MetBefore: Вочы дуэлянта блішчаць ад прадчуваньня, калі ён тлумачыць умовы: "Калі вы пераможаце, я прапаную вам 5000 залатых манет у якасьці пацьверджаньня вашага майстэрства. Калі перамога будзе маёй, то мне хопіць толькі ўганараваньня і параўнаньня свайго майстэрства з вашым." #STR_AcceptNotMetBefore3}
З бляскам азарту ў вачах вы прыймаеце выклік дуэлянта, і на вашым твары з'яўляецца наважная ўхмылка. "Добра - гаворыце вы: я прыймаю вашую прапанову Вітторыё дэ Люка. Давайце зробім гэтую дуэль незабыўнай {MetBefore: яшчэ раз}." #STR_Accept1
Падбадзёрваючы сваіх паплечнікаў, вы загадваеце ім разьбіць лагер на ўзбоччы дарогі, каб ператварыць паляну ў імправізаваную арэну. #illustration: meadow #STR_Accept2
Пасьля завяршэньня ладкаваньня арэны вы выходзіце ў цэнтар, сэрца б'ецца ад прадчуваньня. Вашыя паплечнікі атачылі вас, утварыўшы кола, каб назіраць за дуэльлю, у іх тварах зьмяшаліся хваляваньне і гонар. #STR_Accept3
->enterArena

=deny
~ SetPlayerCustomTag("DeniedVittorio")
Выраз твару Вітторыё застаецца спакойным, але ў кутках вуснаў гуляе ледзь заўважная ўхмылка. #STR_Deny1
"Шкада - адказвае ён, у яго голасе гучыць спагаднасьць: я спадзяваўся знайсьці некага, годнага майго часу, але, здаецца, чуткі пра вашы гераізм былі перабольшанымі". #STR_Deny2
З напышлівай элегантнасьцю Вітторыё дэ Люка завяршае сустрэчу пагрэблівым паклонам, яго рухі выпраменьваюць неаспрэчную перавагу. #STR_Deny3
->END

=enterArena
~ OpenDuelMission()
...
{PlayerWin: Калі суціхае звон мячоў, у паветры раздаюцца радасныя воклічы вашых паплечнікаў, што адлюстроўваюць наступствы вашай нялёгкай перамогі. {SetPlayerCustomTag("DefeatedVittorio")} #STR_PlayerWin1}
{PlayerWin: Вы стаіце ў цэнтры імправізаванай байцоўскай ямы, вашыя грудзі ўздымаюцца ад напругі і трыумфу. Вітторыё дэ Люка, знакаміты Цілійскі дуэлянт, працягвае вам руку ў знак павагі, і яго твар азарае шчырая ўсьмешка. "Выдатны бой" - кажа ён, яго голас поўны захапленьня. #STR_PlayerWin2}
{PlayerWin: Братэрства паміж вашай партыяй і Вітторыё замацоўваецца пасьля ветлівага ўручэньня 5000 залатых манет у знак пацьверджаньня закладу і прызнаньня вашага майстэрства. {GiveGold(5000)} #STR_PlayerWin3} 
{not PlayerWin: Пасьля завяршэньня дуэлі ў паветры лунае сумесь эмоцыяў. Вашыя паплечнікі з маўклівай павагай назіраюць за тым, як Вітторыё дэ Люка, знакаміты Цілійскі дуэлянт, выходзіць пераможцам з бязьлітаснай бойкі. #STR_PlayerLost1}
{not PlayerWin: Вы адыходзіце на крок назад, прызнаючы яго майстэрства ківам захапленьня. Вітторыё стаіць у цэнтры імправізаванай байцоўскай ямы, яго рапіра паблісквае ў згасальным сьвятле, на яго твары зьзяе пераможная ўсьмешка. "Сапраўды, ты грозны супернік - кажа ён, у яго голасе гучыць гонар за сваё дасягненьне: ты гераічна змагаўся, але гэтым разам перамога мая". #STR_PlayerLost2}
Вітторыё дэ Люка паважна разьвітваецца з вашай партыяй. Яго вытанчаная манера паводзін і элегантнасьць словаў застаюцца нязьменнымі, нягледзячы на выйсьце дуэлі. #STR_DuelEnd
->END