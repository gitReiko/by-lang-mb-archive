//Global story tags
# title: Вёска, Паражаная Чумой
# frequency: Special
# development: false
# illustration: village

INCLUDE include.ink

VAR EXTREMESKILLCHECK = 1500
VAR NORMALSKILLCHECK = 150
VAR EASYSKILLCHECK = 50
VAR found_vial = false
VAR suspicious = false
VAR found_passage = false
VAR met_healer = false
VAR found_age = false
VAR DealtWithCultists = false
VAR MadeDealWithCultists = false

->START


===START===
Вы пакідаеце сваю партыю ў лагеры на ўскраіне вёскі і вырашаеце ўвайсьці ў яе ў адзінотку, надзеўшы простую вопратку, каб не прыцягваць увагі. Як толькі вы ўваходзьце ў вёску, на вас абвальваецца дрэнны пах. У паветры вісіць цяжкі пах раскладаньня, зьмяшаны з нудным пахам роспачы. Вузкія пустэчныя вулачкі праходзяць праз паўразбураныя будынкі, фасады якіх знасіліся і патрапаліся ад часу і запусьценьня. Жыхары вёскі, некалі энергічныя і жыцьцярадасныя, зараз ходзяць па вулачках, нібы здані, іх жыцьцёвыя сілы згасаюць пад бязьлітасным напорам невядомай чумы.

Хваравітая імгла ахутвае вёску, надаючы бледнасьць усяму, чаго яна датыкаецца. Дзьверы рыпаюць на іржавых петлях, яркія колеры пяцель пабляклі і аблупіліся. Зачыненыя вокны выдаюць пануючы ўнутры страх, нібы жыхары вёскі замкнуліся на замок. То тут, то там мігатлівыя ліхтары кідаюць слабое сьвятло на надрапаныя на сьценах роспачныя абярэгі - грубыя абярэгі, выгравіраваныя крывёю і попелам, дарэмныя спробы засьцерагчыся ад хваробы.

Сярод запусьценьня некалькі чалавек супрацьстаяць няўмольнаму напору хваробы. Яны рухаюцца мэтліва, у іх вачах мільгае трываласьць. Людзі, якім пашчасьціла выжыць, кідаюць насьцярожаныя погляды, іхнія целы не кранула хвароба, што паразіла іх суседзяў. Іхняе існаваньне, як крохкіх вугольчыкаў сярод цемры, што насоўваецца, намякае на тое, што вёска дагэтуль мае надзею. -> choices

    =choices
    *[Зьвярнуцца за інфармацыяй да вяскоўцаў.] -> QuestionVillagers
    *[Пашпацырыць па вёске, асьцярожна аглядая знакі.] -> ObserveVillage
    *{suspicious}{not found_age} [Распытаць навакольных пра лекара.] -> AskAboutHealer
    *{found_vial || (found_age && met_healer)}[Трохі адпачыць, каб сабрацца з думкамі і прааналізаваць атрыманыя вынікі.] -> GatherThoughts


===QuestionVillagers===
Калі вы падыходзіце да самотнай фігуры сярод змардаваных жыхароў вёскі, у яе вачах мільгае іскра трываласьці. 
Заінтрыгаваныя, вы задаеце пытаньне, якое не дае вам спакою, пытаеце пра чуму. 
Са стомленым выразам твару вясковец адказвае ціхім голасам: "Чума... яна перасьледуе нас ужо некалькі месяцаў. Столькі жыцьцяў загублена, столькі пакут перажыта". Яго голас нясе ў сабе цяжар асабістага досьведу, адлюстроўваючы супальную пакуту, што праймае паветра.
Ён жэстам паказвае на жыльлё вясковага лекара, адзначаючы яго непахісную адданасьць перад тварам роспачы, і кажа: "Шукайце лекара. Ён стаў нашым маяком надзеі, нястомна змагаючыся з гэтай непераадольнай хваробай". ->choices 

    =choices
    *{not met_healer}[Прыслухацца да рады і знайсьці вясковага лекара.] -> AtTheHealers
    *{met_healer}[Вярнуцца да жыльля лекара ў надзеі знайсьці яго ў гэты раз.] -> AtTheHealers

===AskAboutHealer===
Вы вырашаеце непрыкметна распытаць жыхароў вёскі пра лекара.
Завязваючы гутаркі, вы заўважаеце агульную рысу ў іх адказах - лекар заўжды быў слупом вёскі, гэтак доўга, што іншых слупоў, ужо не памятаюць. На яго досьвед апіраліся многімі пакаленьнямі, яго веды перадаваліся з пакаленьня ў пакаленьне.
"Вох, лекар? Колькі я сябе памятаю, ён заўжды быў з намі" - кажа адзін пажылы жыхар вёскі, у яго голасе адчуваецца глыбокая пашана. Яго мудрасьць і лекі выратавалі незьлічонае мноства жыцьцяў, нават мой дзед зьвяртаўся да яго за дапамогай".
Іншы жыхар вёскі дадае: "Так, я быў яшчэ дзіцём, калі ўпершыню ўбачыў яго, і гэта было шмат дзесяцігодзьдзяў таму. Ён дагэтуль увесь час знаходзіцца побач ды дае суцяшэньне хворым і надзею роспачным".
Словы жыхароў вёскі здаюцца вам дзіўнымі, бо калі іх аповеды праўдзівыя, то лекару павінна быць больш за дзевяноста гадоў, а калі вы бачылі яго ў апошні раз, ён выглядаў не старэй за пяцьдзясят. Сумневы закрадаюцца ў самыя куткі сьвядомасьці, як быццам сама рэальнасьць перакруцілася ў гэтай забытай вёсцы.
~found_age = true
->START.choices

===AtTheHealers===
~met_healer=true
Калі вы ўваходзіце ў жыльлё лекара, перад вашымі вачамі разгортваецца карціна, якая адначасна зачароўвае і насьцярожвае. Памяшканьне запоўнена пацыентамі, на іх бледных тварах бачны сьляды чумы. Адны ляжаць на ложках, крывячыся ад болю, іншыя сядзяць у крэслах, з нецярпеньнем чакаючы сваёй чаргі на лячэньне. 

Лекар саўгаецца па пакоі, яго рухі грацыёзныя і мэтанакіраваныя. Ён прамаўляе заспакаяльныя словы, яго дакрананьні далікатныя і ўпэўненыя. На паліцах уздоўж сьцен стаяць незьлічоныя пляшачкі, слоічкі і медычныя прылады. Мігатлівае сьвятло сьвечак адкідае жудасныя цені, адкрываючы пагляду самаадданасьць лекара і прылады яго рамяства.

Сярод мноства лекавых траваў і зёлак вашы погляд спыняецца на нечым незвычайным - дзіўнай склянцы з цёмнай, глейкай вадкасьцю, што стаіць убаку ад астатніх. -> choices 

    =choices
    *[Аглядзець цікавую бутэльку. {print_player_skill_chance("Medicine", EASYSKILLCHECK)}]
        {perform_player_skill_check("Medicine", EASYSKILLCHECK): -> succeed | -> fail}
    *[Распытаць лекара пра бутэльку.] -> inquire
    *{found_vial}[Супрацьдзейнічаць лекару.] -> ConfrontHealer
    *{found_vial}[Пакуль пакінуць адкрыцьцё пры сабе і працягнуць сваё расьсьледаваньне.] -> START.choices
    *[Пакінуць.] -> START.choices

    =succeed
    Па меры нарастаньня пачуцьця трывогі, вы аглядаеце бутэльку, вашае нарыхтаванае вока распазнае ў яго зьмесьціве рэчыва, якое насамрэч з'яўляецца - падступным узбуджальнікам хваробы, магутную і гідкую матэрыю, якая падсілкоўвае саму чуму, што раз'ядае вёску.
    ~found_vial = true
    ->choices
    
    =fail
    Вы разглядаеце бутэльку з разгубленым выразам твару і ня можаце вызначыць яе прызначэньне і зьмесьціва, бо не валодаеце патрэбнымі медычнымі ведамі дзеля распазнаньня сапраўднай прыроды. -> choices

    =inquire
    Вы накіроўваеце свой пагляд на лекара і наўпрост пытаеце пра незвычайную бутэльку. 
    "Якое прызначэньне мае гэтая бутэлька? Яе зьмесьціва не падобнае на ніводную вядомую мне рэч." - пытаеце вы, у вашым тоне адчуваецца цікаўнасьць і асьцярожнасьць. 
    Лекар сустракае вашы погляд, у яго вачах на імгненьне мільгае занепакоенасьць, але потым ён адказвае супакойным голасам: "Вой, у гэтай бутэльцы ўтрымваецца магутны экстракт рэдкай травы - ключавы інгрэдыент магутнага дэзынфектавальнага сродку. Ён дапамагае стрымліваць распаўсюджваньне хваробы, забясьпечвае бясьпеку як хворых, гэтак і здаровых" - яго словы суправаджаюцца спакойнай усьмешкай, што маскуе схаваныя боязі. 
    ~suspicious = true
    -> choices

===ObserveVillage===

Вы ідзяце па пустэльных вуліцах, уважліва аглядаеце навакольле ў пошуках адзнак, якія маглі выявіць праўду пра чуму і чуткі пра дзейнасьць культыстаў. Сярод паўразбураных будынкаў і пакутуючых жыхароў вы заўважаеце ледзь заўважныя дэталі, якія выклікаюць у вас цікавасьць.

У вочы кідаецца выцьвілы знак, выгравіраваны на паўразбуранай сьцяне. Ён напамінае сымбаль бога хаосу Нургла і намякае на магчымую сувязь з чуткамі пра культыстаў. Убачыўшы чуму, вы ўжо здагадаліся пра гэтае, але адной яе наяўнасьці недастаткова для пацьверджаньня праўды.

Сярод запусьценьня вы заўважаеце незвычайную заканамернасьць. Вашую ўвагу прыцягваюць асобныя людзі, якія не паддаюцца хваробе, што паразіла ўсіх астатніх. Мужчыны сярэдняга веку ад трыццаці да сарака гадоў, яны выпраменьваюць незвычайнае здароўе і жыцьцёвую сілу. Іх моцны, мускулісты целасклад рэзка кантрастуе з навакольнымі крохкімі і зьнясіленымі фігурамі. Іхнія шчокі палаюць здаровым румянкам, сьвецяцца жыцьцёвай сілай, а вочы іскрацца пруткасьцю і моцай. Здаецца, што сама іх наяўнасьць выпрамяняе жыцьцё сярод змроку.

Немагчыма не ўразіцца іх дабрабыту. Іх вылучнае здароўе выклікае ў вас пытаньні: што дае ім такую незвычайную трываласьць? Ці маецца гэтаму натуральнае тлумачэньне, ці за іхняй уяўнай непаражальнасьцю крыецца штосьці большае? -> choices

    =choices
    *[Назіраць за штодзённым жыцьцём здаровых жыхароў вёскі.] -> ObserveRoutine

===ObserveRoutine===
Зацікавіўшыся незвычайным здароўем мужчынаў сярэдняга веку ў вёсцы, вы вырашылі панаглядаць за іхнім распарадкам дня на працягу некалькіх дзён. Дасьледаваньне прыводзіць вас да цікавага адкрыцьця: шторанку здаровыя жыхары вёскі неадменна зьбіраюцца ў сьціплым жыльлі вясковага лекара.
Гэта выклікае цікаўнасьць, бо ім не патрабуецца аніякага лячэньня, але пры гэтым бавяць значную колькасьць часу разам з лекарам. -> choices
    
    =choices
    *[Увайсьці ў жыльлё лекара падчас знаходжаньня большасьці здаровых жыхароў вёскі ўнутры.] -> AtTheHealersAgain

===AtTheHealersAgain===
Цікаўнасьць бярэ верх, і вы вырашаеце скарыстацца выпадкам і ўвайсьці ў жыльлё лекара {met_healer: зноў} падчас збору ўсіх здаровых жыхароў вёскі. Як толькі вы пераступаеце парог, вас сустракае жудаснае гледзішча. Прыстанак лекара цьмяна асьветлены, напоўнены водарам траваў і пахошчаў. На адным боку пакоя ў цяжкім стане ляжаць некалькі жыхароў вёскі, якія стогнуць ад болю, іхнія целы разбураны чумой. 
Але ні лекара, ні здаровых жыхароў, якіх вы бачылі некалькі хвілін таму, ня бачна.
~suspicious = true
->choices

    =choices
    *[Агледзецца ў пошуках падказкі, куды яны маглі зьнікнуць.{print_player_skill_chance("Scouting", EASYSKILLCHECK)}]
        {perform_player_skill_check("Scouting", EASYSKILLCHECK): -> succeed | -> fail}
    *{found_passage}[Не разважаючы спусьціцца пад зямлю.] -> Descend
    *{not found_passage}[Адмовіцца ад пошукаў і сысьці.] -> START.choices

    =succeed
    Вашы погляд кідаўся па пакоі ў пошуках хоць якой зачэпкі, якая магла б патлумачыць іх раптоўнае зьнікненьне. Уздоўж сьцен стаяць паліцы, упрыгожаныя склянкамі, бутэлькамі з зельлямі і старажытнымі кнігамі па розных медычных практыках. У пакоі адчуваецца дзіўная напруга, як быццам у ёй захоўваецца таямніца, якая чакае разгадкі.
    Па меры таго як вы асьцярожна прасоўваецеся далей, вашую ўвагу прыцягвае патайны праход, схаваны за цяжкім габеленам. Інстынктыўна вы падыходзіце бліжэй, сэрца б'ецца ў прадчуваньні. Здаецца, што праход вядзе пад зямлю, у нязьведаную цемру, якая гэтак і вабіць адкрыць свае таямніцы.
    ~found_passage=true
    ->choices
    
    =fail
    Вы абшнырваеце вачамі пакой у пошуках хоць якой зачэпкі, якая магла б патлумачыць іх раптоўнае знікненьне. Вы прыдзірліва аглядаеце кожны куток, праводзіце рукамі па паліцах і сьценах, спрабуючы знайсьці схаваны праход. Аднак, як ні імкніцеся, выяўнага шляху да разгадкі таямніцы не знаходзіце.
    Расчараваньне і зьбянтэжанасьць авалодваюць вамі, і вы ня можаце не пачувацца агаломшаным сітуацыяй. Лекар і здаровыя жыхары вёскі нібы бясьсьледна зьніклі, і ў вас засталося больш пытаньняў, чым адказаў. Ці маглі яны адчуць вашую наяўнасьць і высьлізнуць неўпрыкмет?->choices

===GatherThoughts===
Вы знаходзіце хвіліну дзеля перадышкі, каб сабрацца з думамі, аддаляецеся ў ціхі куток вёскі, панадалей ад чужых вачэй, і скрупулёзна вывучаеце сабраныя на гэты момант доказы.
Здаровыя жыхары вёскі, нібы не кранутыя чумой, працягваюць заставацца таямніцай. Іхняе моцнае здароўе, жывая энергія і невытлумачальны імунітэт ставяць у тупік розум. Яны - увасабленьне жыцьця на фоне роспачы, што ахапіла вёску.
Але іхняя сувязь з лекарам выклікае больш пытаньняў, чым адказаў. Калі лекар сапраўды клапоціцца пра вёску на працягу шматлікіх пакаленьняў, то ён павінен быць трухлявым, пажылым чалавекам, але гэта далёка не так. У апошні раз, калі вы сустракалі яго, ён выглядаў мужчынам у росквіце сілаў, які не глядзіць на плынь часу.
Выяўленьне знака Нургла, выгравіраванага на паўразбуранай сьцяне, намякае на магчымую сувязь з чуткамі пра культыстаў. Наяўнасьць такога знака ў вёсцы кідае злавесны цень на яе і без таго змрочную атмасьферу.
Апроч таго, падазроная бутэлька, знойдзеная ў жыльлі лекара, не дае вам жыцьця. {not found_vial: Яе зьмесьціва застаецца незразумелым, а тлумачэньні лекара выклікаюць сумневы ў яе сапраўдным прызначэньні. Ці можа яна быць узбуджальнікай заразы, ці гэты сапраўды нявінны сродак, як сьцьвярджаецца?}{found_vial: Вы цалкам ўпэўнены, што зьмесьціва бутэлькі выкарыстоўваецца дзеля распальваньня эпідэміі. Зь якой мэтай? Гэта яшчэ трэба высьветліць.}
У галаве зараджаецца падозраньне, што ледзяніць душу. Ці можа быць так, што сам лекар неяк зьвязаны з чумой і чуткамі пра культыстаў? Можа, пад яго зычлівым фасадам хаваецца нешта таемнае? З новай сілай вы вырашаеце нарэшце сустрэцца з лекарам твар у твар.
->ConfrontHealer

===ConfrontHealer===
{came_from(-> GatherThoughts): Набраўшыся рашучасьці, вы зноў уваходзіце ў жыльлё лекара з вострымі ад зноў здабытых ведаў вачамі.} Калі вы набліжаецеся, вочы лекара сустракаюцца з вашымі. Настае момант маўклівага прызнаньня - усьведамленьня, што праўда адкрыта.
Адчуўшы цяжар вашых падозраў, твар лекара перакручваецца ад страху і роспачы. Не кажучы ні слова, ён паварочваецца і кідаецца ў далёкі канец пакоя, робячы роспачную спробу схавацца ад вашага пільнага погляду.
"Пачакайце!" - крычыце вы і кідаецеся ў пагоню, калі ён далазіць да задняй часткі жыльля. Хуткім рухам ён адкрывае патайныя дзьверы, расхінае іх і зьнікае ў глыбіні. Вы, не разважаючы, спускаецеся па вузкіх прыступках у непраглядную цемру. -> choices
    
    =choices
    *[Бяз роздумаў, вы рушыце ўсьлед за ім, спускаецеся па вузкіх ступенях у непраглядную цемру.] -> Descend
    

===Descend===
~ OpenCultistLairMission("TOR_nurgle_lair_001")
...
{MadeDealWithCultists && not DealtWithCultists: Вочы лідара культу зіхацяць урачыстасьцю і задавальненьнем, калі ён адчувае вашую згоду. Яго напеў набывае гіпнатычную ўласьцівасьць, рэзануючы ў вашай сьвядомасьці, нібы усталёўваючы зь ім непарыўную сувязь. Адчуваньні, што ахапляюць вас, дзіўныя і трывожныя, быццам тагасьветная сіла пачынае ўплятацца ў вашую існасьць. }
{MadeDealWithCultists && not DealtWithCultists: Пачынаецца неперадатнае ператварэньне, падобнае на разгортваньне мары, вытканай з рэальнасьці. Прыліў энергіі разьліваецца па венах, бадзёрыць цела і здымае стомленасьць. Болі і раны нібы зацягваюцца, пакідаючы пасьля сябе адчуваньне новай жыцьцёвай сілы.}
{MadeDealWithCultists && not DealtWithCultists: Прыняўшы Дарунак, вы пакідаеце пакой з цяжарам прынятага разьвязку. Вёска зьнікае ўдалечыні, у вушах гучаць прыглушаныя крыкі яе жыхароў.}
{MadeDealWithCultists && not DealtWithCultists: Вашы шлях працягваецца, і перасьледуе водгаласкамі прынятага вамі рашэньня. Дарунак Нургла даў вам сілу і жыцьцёвую энергію, але ён жа зьвязаў вас з будучымі, няпэўнымі і апанурымі мукамі сумленьня.}
{not DealtWithCultists && not MadeDealWithCultists: Вашыя ўдары адбіваюцца, а абарона апынаецца марнай. Злавеснае майстэрства лідара культу перасягае ўсе вашыя чаканьні, а апошні, руйнавальны ўдар пазбаўляе вас сіл. Цемра авалодвае вашымі пачуцьцямі, а сьвядомасьць зьнікае.}
{not DealtWithCultists && not MadeDealWithCultists: Праз некалькі гадзін, а можа быць, і цэлую вечнасьць, вы прачынаецеся сярод шуму і гаму пакутных. Вашае цела баліць, яно пабіта і зранена, водгаласкі паразы аддаюцца ў кожнай частцы цела. Дэзарыентаваны і зранены, вы выяўляеце сябе сярод жыхароў вёскі, якіх вы спрабавалі выратаваць.}
{not DealtWithCultists && not MadeDealWithCultists: Ледзь поўзаючы па пустэчы, вы арыентуецеся сярод пакутных душ, кожны стогн і ўздых якіх сьведчыць пра бязьлітасную хватку чумы. Усьведамленьне свайго няшчасьця гняце вас, густ паразы горкі на языке.}
{not DealtWithCultists && not MadeDealWithCultists: Урэшце вам шчасьціць выбрацца з гэтага вартага жалю месца, і вашае пераламанае цела вядзе вас назад у лагер, дзе схавалася вашая партыя. Вочы паплечнікаў сустракаюцца з вашымі вачамі, поўнымі турботы і спагады, калі яны ўсьведамляюць маштабы вашай паразы.}
{not DealtWithCultists && not MadeDealWithCultists: Не кажучы ні слова, вы зьбіраеце рэшткі сіл і рашучасьці ды пакідаеце вёску зь яе змучанымі жыхарамі. Успаміны пра сутыкненьне з лідарам культу, пра няшчасьце ў змаганьні з чумой перасьледуюць вас што ні крок.}
{not DealtWithCultists && not MadeDealWithCultists: Пакуль вы рушыце, вёска зьнікае з гарызонту, а яе спадчына пакут назаўжды захоўваецца ў вашай сьвядомасьці. Цяжар паразы душыць вас, незабыўна напамінаючы пра цемру, якую вы не змаглі расьсеяць.}
{DealtWithCultists && not MadeDealWithCultists: Параза лідара культу рэхам пракацілася па падземным логаве і дакацілася да самага сэрца вёскі, заражанай чумой. Выйдучы вонкі, вы бачыце, як хворыя жыхары выходзяць са сваіх хат, выказваюць недавер і зноў здабытую надзею.}
{DealtWithCultists && not MadeDealWithCultists: Вёска пачынае ачуньваць, яе жыхары марудна, але пэўна апраўляюцца ад пакут, якія некалі зьвязвалі іх. Калі вашая партыя ўз'ядноўваецца з вамі, на іх тварах адлюстроўваецца сумесная падзяка вёскі.}
{DealtWithCultists && not MadeDealWithCultists: Выканаўшы сваю місію, вы сыходзіце з вёскі, пакідаючы пасьля сябе грамаду, што ідзе па шляху аднаўленьня. Памяць пра вашую перамогу, пра змаганьне з цемрай застанецца ў сэрцах жыхароў вёскі як сьведчаньне сілы чалавечага духу.}

->END